apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'


findbugs {
    excludeFilter = file("$rootProject.projectDir/src/main/findbugs/findbugs-excludes.xml")
}


group = 'org.injava.lang.designbycontract'
version = '0.0.1-SNAPSHOT'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

signing {
    sign configurations.archives
}



sourceCompatibility = 1.2
targetCompatibility = 1.2

tasks.withType(Compile) {
    options.compilerArgs << "-Xlint:-options"
}

compileTestJava { sourceCompatibility = '1.7'; targetCompatibility = '1.7' }

jar {
    manifest {
        attributes 'Implementation-Title': 'org.injava.lang.DesignByContract', 'Implementation-Version': version
    }
}

repositories {
  mavenCentral()
}

dependencies {
  testCompile 'junit:junit:4.11'
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

	    def repositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/" // Sonatype OSS snapshots
            // TODO: Automate CI/CD deployments
            // def repositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/" // productive URL (staging)
            repository(url: repositoryUrl) {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
               name 'DesignByContract'
               packaging 'jar'
               description 'DesignByContract provides EIFFEL-inspired support for the design-by-contract approach in Java'
               url 'http://gradlefx.github.com/'

               scm {
                   url 'scm:git@github.com:jnook/org.injava.lang.DesignByContract.git'
                   connection 'scm:git@github.com:jnook/org.injava.lang.DesignByContract.git'
                   developerConnection 'scm:git@github.com:jnook/org.injava.lang.DesignByContract.git'
               }

               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'jensbirgerhahn'
                       name 'Jens Birger Hahn'
                   }
               }
           }
        }
    }
}
